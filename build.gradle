buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:0.12.2'
  }
}

//subprojects.each { println it.path }
subprojects {
  repositories {
    mavenCentral()
  }
}

configure(subprojects.findAll { it.path.startsWith(':cyclestreets') }) {
  apply plugin: 'com.android.application'

  android {
    Properties appConfig = new Properties()
    appConfig.load(new FileInputStream(file("${rootDir}/config.properties")))

    compileSdkVersion appConfig['sdkVersion'].toInteger()
    buildToolsVersion appConfig['toolsVersion']

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_7
      targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
      minSdkVersion appConfig['minSdkVersion'].toInteger()
      targetSdkVersion appConfig['targetSdkVersion'].toInteger()
      versionCode appConfig['versionCode'].toInteger()
      versionName appConfig['versionName']
    } // defaultConfig

    def licenseDetails = file("${rootDir}/license.properties");

    if (licenseDetails.exists()) {
      signingConfigs {
        release {
          Properties props = new Properties()
          props.load(new FileInputStream(licenseDetails))

          storeFile file(props['key.store'])
          storePassword props['key.password']
          keyAlias props['key.alias']
          keyPassword props['key.password']
        }
      } // signingConfigs

      buildTypes {
        release {
          signingConfig signingConfigs.release
        }
      } // buildTypes
    } // lincenseDetails.exists

    packagingOptions {
      exclude 'META-INF/LICENSE'
      exclude 'META-INF/LICENSE.txt'
      exclude 'META-INF/DEPENDENCIES'
      exclude 'META-INF/NOTICE'
      exclude 'META-INF/NOTICE.txt'
    } // packagingOptions
  } // android
} // cyclestreets apps

configure(subprojects.findAll { it.path.startsWith(':libraries:') }) {
  apply plugin: 'com.android.library'

  android {
    Properties appConfig = new Properties()
    appConfig.load(new FileInputStream(file("${rootDir}/config.properties")))

    compileSdkVersion appConfig['sdkVersion'].toInteger()
    buildToolsVersion appConfig['toolsVersion']

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_7
      targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
      minSdkVersion appConfig['minSdkVersion'].toInteger()
      targetSdkVersion appConfig['targetSdkVersion'].toInteger()
      versionCode appConfig['versionCode'].toInteger()
      versionName appConfig['versionName']
    }
  }
}